plugins {
  id 'org.ec4j.editorconfig' version '0.0.3'
  id 'org.xtext.builder' version '2.0.8'
}

editorconfig {
  excludes = [
    '**/generated',
    '**/xtext-gen',
    '**/bin',
    '**/.*',
    'gradlew*'
  ]
}

check.dependsOn(editorconfigCheck)

subprojects {
  group = 'org.metafacture.fix'
  version = '1.0.0-SNAPSHOT'

  ext {
    versions = [
      'ace':            '1.3.3',
      'jetty':          '9.4.14.v20181114',
      'jquery':         '3.3.1-1',
      'junit_jupiter':  '5.4.2',
      'junit_platform': '1.4.2',
      'metafacture':    'master',
      'mockito':        '2.27.0',
      'requirejs':      '2.3.6',
      'slf4j':          '1.7.21',
      'xtext':          '2.17.0'
    ]
  }

  apply plugin: 'checkstyle'
  apply plugin: 'eclipse'
  apply plugin: 'java'
  apply plugin: 'org.xtext.builder'

  apply from: "${rootDir}/gradle/source-layout.gradle"

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  repositories {
    jcenter()
  }

  dependencies {
    compile platform("org.eclipse.xtext:xtext-dev-bom:${versions.xtext}")
  }

  configurations.all {
    exclude group: 'asm'
  }

  checkstyle {
    toolVersion '8.30'
  }

  tasks.withType(JavaCompile) {
    'all -processing -rawtypes -serial'.split().each {
      options.compilerArgs << "-Xlint:${it}"
    }

    options.compilerArgs << '-Werror'
  }
}

task installServer(type: Copy) {
	dependsOn(':org.metafacture.fix.ide:installDist')
    from "org.metafacture.fix.ide/build/install/xtext-server"
    into "vscode-extension/xtext-server"
}
